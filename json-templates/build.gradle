plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin:"java"
apply plugin:"groovy"
apply plugin: 'maven-publish'

group "org.grails.plugins"

ext {
    gradleWrapperVersion = project.gradleWrapperVersion
    userOrg = 'grails'
    repo = 'plugins'
    isGrailsPlugin = false
    projectDesc = "JSON Views Templates"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile project(':views-json')
    compileOnly "org.grails:grails-plugin-rest:$grailsVersion"
    compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"
    compileOnly "org.grails:grails-datastore-gorm-mongodb:5.0.6.RELEASE"
}

task('compileViews', type:JavaExec) {
    inputs.files 'src/templates'
    outputs.dir 'build/classes/groovy/main'
    main "grails.plugin.json.view.JsonViewCompiler"
    classpath configurations.compile + configurations.compileOnly
    args("src/templates", "build/classes/groovy/main", "1.8", ' ', ' ', "none", "UTF-8")
}

task sourcesJar(type:Jar) {
    classifier = 'sources'
    from 'src/templates'
}

// no javadocs, just a workaround
task javadocJar(type:Jar) {
    classifier = 'javadoc'
    from 'src/templates'
}

classes.dependsOn(compileViews)

// Used for publishing to central repository, remove if not needed
apply from:'../publishing/grailsCentralPublishing.gradle'
apply from:'../publishing/bintrayPublishing.gradle'
