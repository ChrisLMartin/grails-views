buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

group "org.grails.plugins"

apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
apply plugin: "org.grails.grails-plugin-publish"
apply plugin: 'optional-base'

sourceCompatibility = 1.8
targetCompatibility = 1.8

grailsPublish {
    userOrg = 'grails'
    githubSlug = 'grails/grails-views'
    license {
        name = 'Apache-2.0'
    }
    title = "JSON Views Plugin"
    desc = "Adds support for defining views in JSON"
    developers = [graemerocher:"Graeme Rocher"]
}

bintray {
    pkg {
        desc = "Grails Views"
        name = 'grails-views'
    }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if( details.requested.group == 'org.grails' &&
                details.requested.name.startsWith('grails-datastore')) {
            details.useVersion("$gormVersion")
        }
    }
}

dependencies {
    compile "org.grails:grails-core:$grailsVersion"

    compile project(":views-core")

    testCompile "org.grails:grails-web-testing-support:$testingSupportVersion"
    testCompile "org.grails:grails-gorm-testing-support:$testingSupportVersion"
    testCompile "org.grails:grails-datastore-gorm-hibernate5"
    testRuntime "org.javassist:javassist:3.18.1-GA"

    console "org.grails:grails-console"
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

jar {
  exclude "*.gson"
}
